<!doctype html>
<html>
    <head>
        <meta charset="utf-8" />
        <meta name="viewport" content="width=device-width, initial-scale=1" />
        <title>trying to draw a square - gbadev.org forum archive</title>
        <link rel="stylesheet" href="static/pure-min.css" />
        <link rel="stylesheet" href="static/main.css" />
    </head>
    <body>
        <h1>gbadev.org forum archive</h1>

        This is a mirror of the content originally found on forum.gbadev.org
        (now offline), salvaged from Wayback machine copies. <br />

        <h2>ASM > trying to draw a square</h2>
<div id="posts">
<div class="post">
    <h4>#153284 - yaazz - Thu Mar 27, 2008 6:22 pm</h4>
    <div class="postbody"><span class="postbody">I am trying to write a simple asm program to draw a square on the screen. I am using the concept of Y * W + X will give you the correct index but it doesnt seem to work. Does anyone see any obvious problem with my code?
<br/>
<br/>
</span><table align="center" border="0" cellpadding="3" cellspacing="1" width="90%"><tr> <td><span class="genmed"><b>Code:</b></span></td> </tr> <tr> <td class="code"> 
<br/>
.arm
<br/>
.text
<br/>
.global main
<br/>
<br/>
main:
<br/>
mov r0, #0x4000000
<br/>
mov r1, #0x400 
<br/>
         
<br/>
add r1, r1, #3   
<br/>
            
<br/>
str r1, [r0] 
<br/>
<br/>
      
<br/>
mov r0, #0x6000000  @ address of VRAM
<br/>
mov r1, #0x6500     @ some pinkish color
<br/>
add r1, r1, #0x9E
<br/>
<br/>
mov r2, #0xF0       @ the width of the screen
<br/>
mov r3, #0x64       @X=100
<br/>
mov r4, #0x32       @Y=50
<br/>
mov r5, #0x32       @W &amp; H = 50
<br/>
<br/>
<br/>
<br/>
<br/>
loop1:
<br/>
   mul r6, r4, r5   @curposition = Y * Width
<br/>
   add r6, r6, r3   @curposition += X
<br/>
<br/>
loop2:
<br/>
   strh r1, [r0,r6]  @ will store the 16bit value in r1 into address in r0, then
<br/>
    add r6,r6,#2      @ add 16bits to r6
<br/>
    subs r5, r5, #0x1 @ subtract 1 from W
<br/>
    bne loop2         @ if W!=0 goto loop2   
<br/>
    
<br/>
    add r1, r1, #0x1  @make the color something else
<br/>
    mov r5, #0x32     @Reset the W variable
<br/>
    add r4, r4, #1    @add 1 to Y variable
<br/>
    cmp r4, #100       @compare Y to 100
<br/>
    bne loop1         @if not equal goto loop 1
<br/>
    
<br/>
infin:                @ an infinite loop
<br/>
   
<br/>
<br/>
b infin
<br/>
</td> </tr></table><span class="postbody"></span><span class="gensmall"></span></div>    
</div>
<div class="post">
    <h4>#153289 - Cearn - Thu Mar 27, 2008 8:06 pm</h4>
    <div class="postbody"><span class="postbody">First, you should use the width of the screen to find the scanline addresses, not the width of your rectangle. Second, there is a difference between a bitmap's width and its pitch. The width is the number of pixels per scanline; the pitch is the number of bytes per scanline. The pitch for mode 3 is 480, not 240.
<br/>
<br/>
Something similar is true for how you use X: X=100 actually means a byte-offset of 2*100 for 16bit bitmaps. 
<br/>
<br/>
C accounts for the difference between (u16*) and (u8*) automatically, but assembly doesn't.</span><span class="gensmall"></span></div>    
</div>
<div class="post">
    <h4>#153293 - Miked0801 - Thu Mar 27, 2008 10:33 pm</h4>
    <div class="postbody"><span class="postbody">You didn't say if you wanted Thumb or Arm.  This is ARM, but could be converted to thumb with 2 extra instructions.
<br/>
<br/>
</span><table align="center" border="0" cellpadding="3" cellspacing="1" width="90%"><tr> <td><span class="genmed"><b>Code:</b></span></td> </tr> <tr> <td class="code">
<br/>
    mov r0, #0x6000000  @ address of VRAM 
<br/>
    mov r1, #0x6500     @ some pinkish color 
<br/>
    add r1, r1, #0x9E     
<br/>
<br/>
    mov r2, #0xF0       @ Width of screen in pixels 
<br/>
    mov r3, #0x64       @ X=100 pixels
<br/>
    mov r4, #0x32       @ Y=50
<br/>
    mov r5, #0x32       @ Width/height
<br/>
<br/>
    mul  r4,r4,r5       @  = y * width
<br/>
    add  r4,r4,r3       @ add in X offset
<br/>
    add  r0,r0,r4,lsl#1 @ Add to base pointer *2 for width
<br/>
<br/>
    sub  r2, r2,r5      @ how many pixels to next row
<br/>
    mov r4, r5          @ Setup lower loop Height counter
<br/>
<br/>
loop1:
<br/>
    mov r3, r5          @ Setup width counter
<br/>
<br/>
loop2:
<br/>
    strh r1,[r0]!       @ Store pixel and update address
<br/>
    subs r3,r3,#1       @ loop till done
<br/>
    bne loop2           @ ...
<br/>
<br/>
    add r1,r1,#1        @ Update pixel color
<br/>
    add r0,r0,r2,lsl#1  @ add in pixel to next row*2 for 16-bits
<br/>
    subs  r4,r4,#1      @ loop till done
<br/>
    bne loop1           @ ...
<br/>
<br/>
</td> </tr></table><span class="postbody">
<br/>
<br/>
Untested, but it show how to deal with 16/8 bit conversions on the fly which your code does not handle.  I'm also assuming that your setup values are passed in and the values cannot be used as immediates below like you are doing with your #100.  I also use 1 register less, but that matters little.
<br/>
<br/>
For real speed, you should draw large runs with stm and half word alignments on the beginning/end.  If you were allowed to know the size of your rect before hand was always 50 pixels, the inner loop becomes lightning fast.
<br/>
<br/>
Finally, if you are drawing in an 8-bit mode, then the shifts need to swap around a bit, but the general idea still works.</span><span class="gensmall"></span></div>    
</div>
<div class="post">
    <h4>#153324 - yaazz - Fri Mar 28, 2008 3:40 pm</h4>
    <div class="postbody"><span class="postbody">oops the problem was i was using the square width and not the screen width.
<br/>
<br/>
Also, is there any way to make variables in asm?</span><span class="gensmall"></span></div>    
</div>
<div class="post">
    <h4>#153326 - tepples - Fri Mar 28, 2008 4:28 pm</h4>
    <div class="postbody"><span class="postbody">In assembly language on x86 or ARM, you move the stack pointer down to make space for local variables.
<br/>
<br/>
<a class="postlink" href="http://www.cs.cornell.edu/courses/cs414/2001fa/armcallconvention.pdf" target="_blank">See how procedure calls work in ARM and Thumb</a> (pdf)<br/>_________________<br/>-- Where is he?
<br/>
-- Who?
<br/>
-- You know, the human.
<br/>
-- I think he moved to Tilwick.</span><span class="gensmall"></span></div>    
</div>
<div class="post">
    <h4>#153411 - yaazz - Sat Mar 29, 2008 9:31 pm</h4>
    <div class="postbody"><span class="postbody">A quick search for Arm instruction sets tell me PUSH and POP can only be used in thumb mode? 
<br/>
If this is true how do I use the stack in ARM mode?</span><span class="gensmall"></span></div>    
</div>
<div class="post">
    <h4>#153412 - tepples - Sat Mar 29, 2008 9:34 pm</h4>
    <div class="postbody"><span class="postbody"></span><table align="center" border="0" cellpadding="3" cellspacing="1" width="90%"><tr> <td><span class="genmed"><b>yaazz wrote:</b></span></td> </tr> <tr> <td class="quote">A quick search for Arm instruction sets tell me PUSH and POP can only be used in thumb mode? 
<br/>
If this is true how do I use the stack in ARM mode?</td> </tr></table><span class="postbody">
<br/>
The ARM equivalents of the PUSH and POP instructions are the STMFD and LDMFD instructions. Please read the PDF to see how.<br/>_________________<br/>-- Where is he?
<br/>
-- Who?
<br/>
-- You know, the human.
<br/>
-- I think he moved to Tilwick.</span><span class="gensmall"></span></div>    
</div>
<div class="post">
    <h4>#153415 - Dwedit - Sat Mar 29, 2008 10:58 pm</h4>
    <div class="postbody"><span class="postbody">example in arm:
<br/>
stmfd sp!,{r0,r1,r2-r7,lr}  @push
<br/>
<br/>
ldmfd sp!,{r0,r1,r2-r7,pc} @pop
<br/>
<br/>
note that you can't push LR and pop PC if you intend on returning to thumb mode...<br/>_________________<br/>"We are merely sprites that dance at the beck and call of our button pressing overlord."</span><span class="gensmall"></span></div>    
</div>
<div class="post">
    <h4>#153451 - yaazz - Sun Mar 30, 2008 10:37 pm</h4>
    <div class="postbody"><span class="postbody">I read through the procedure call standard, and although I learned a few things I didnt already know, I didnt see any mention of the STMFD and LDMFD instructions... Where should I be looking?</span><span class="gensmall"></span></div>    
</div>
<div class="post">
    <h4>#153479 - Cearn - Mon Mar 31, 2008 5:11 pm</h4>
    <div class="postbody"><span class="postbody">devkitArm accepts push and pop  for ARM code as well, but it may be incompatible with other/older assemblers.
<br/>
<br/>
There's a very quick run-down of ARM/Thumb assembly in Tonc <a class="postlink" href="http://www.coranac.com/tonc/text/asm.htm" target="_blank">here</a>. It contains the most important information and covers some GBA-specific items, but the information is densely packed. At least get the documents pointed to at the top; the quick-references in particular are very useful.
<br/>
<br/>
Some other, more extensive documents:
<br/>
<a class="postlink" href="http://www.ee.ic.ac.uk/pcheung/teaching/ee2_computing/" target="_blank">http://www.ee.ic.ac.uk/pcheung/teaching/ee2_computing/</a>
<br/>
<a class="postlink" href="http://www.heyrick.co.uk/assembler/" target="_blank">http://www.heyrick.co.uk/assembler/</a>
<br/>
<a class="postlink" href="http://www.peter-cockerell.net/aalp/html/frames.html" target="_blank">http://www.peter-cockerell.net/aalp/html/frames.html</a>
<br/>
<br/>
The document tepples pointed to is somewhat outdated. A more recent version is this one: <a class="postlink" href="http://www.arm.com/miscPDFs/8031.pdf" target="_blank">AAPCS</a></span><span class="gensmall"></span></div>    
</div>
<div class="post">
    <h4>#153668 - yaazz - Thu Apr 03, 2008 5:59 pm</h4>
    <div class="postbody"><span class="postbody">from TONC
<br/>
<br/>
.Lpool:
<br/>
    .word   0x06010000
<br/>
    .word   far_var
<br/>
<br/>
@ Shorthand: use ldr= and GCC will manage the pool for you
<br/>
    ldr     r0,=0x06010000  @ Load a value
<br/>
    ldr     r0,=far_var     @ Load far_var's address
<br/>
    ldr     r0, [r0]        @ Load far_var's contents
<br/>
<br/>
Is this far_var word just pseudocode? because if I try to do this to store a variable it wont compile
<br/>
<br/>
Im not really trying to make local vareiables, I want to make global variables</span><span class="gensmall"></span></div>    
</div>
<div class="post">
    <h4>#153671 - Cearn - Thu Apr 03, 2008 6:44 pm</h4>
    <div class="postbody"><span class="postbody">The point of that particular note wasn't about how to create variables, but about two methods that you can use to load large values and addresses (like the addresses of variables) into registers.
<br/>
This code
<br/>
</span><table align="center" border="0" cellpadding="3" cellspacing="1" width="90%"><tr> <td><span class="genmed"><b>Code:</b></span></td> </tr> <tr> <td class="code">    ldr     r0, .Lpool      @ Load a value
<br/>
    ldr     r0, .Lpool+4    @ Load far_var's address
<br/>
    ldr     r0, [r0]        @ Load far_var's contents
<br/>
.Lpool:
<br/>
    .word   0x06010000
<br/>
    .word   far_var
<br/>
</td> </tr></table><span class="postbody">
<br/>
and this
<br/>
</span><table align="center" border="0" cellpadding="3" cellspacing="1" width="90%"><tr> <td><span class="genmed"><b>Code:</b></span></td> </tr> <tr> <td class="code">    ldr     r0,=0x06010000  @ Load a value
<br/>
    ldr     r0,=far_var     @ Load far_var's address
<br/>
    ldr     r0, [r0]        @ Load far_var's contents
<br/>
</td> </tr></table><span class="postbody">
<br/>
are functional equivalents. The line ".word far_var" doesn't create a far_var variable, it creates a space containing the address of far_var. Sorry if this was unclear.
<br/>
<br/>
To create the variable itself, you have to make a label followed by some data declarations. For example, this creates a global halfword called far_var and initializes it with 12:
<br/>
</span><table align="center" border="0" cellpadding="3" cellspacing="1" width="90%"><tr> <td><span class="genmed"><b>Code:</b></span></td> </tr> <tr> <td class="code">@ Equivalent of 
<br/>
@ u16 far_var= 12;
<br/>
<br/>
    .data                @ Data section (IWRAM)
<br/>
    .align 1             @ Alignment for a halfword
<br/>
    .global far_var      @ Make it global
<br/>
far_var:
<br/>
    .hword   12          @ Allocate a halfword containing 12
<br/>
</td> </tr></table><span class="postbody">
<br/>
Creation of variables is covered in more detail in the <a class="postlink" href="http://www.coranac.com/tonc/text/asm.htm#sec-gas" target="_blank">GNU assembler section</a>. Note that other assemblers (the official ARM assembler, for example) may do things differently.
<br/>
<br/>
A decent way to get a feel for ARM asm and how the assembler expects things is to look at the compiler-generated assembly. Create some C code and add "-save-temps" to CFLAGS; the corresponding assembly will be in an .s file in the build directory. It may help to remove the -g flag when doing so: the debugging information makes the asm harder to read.</span><span class="gensmall"></span></div>    
</div>
<div class="post">
    <h4>#153681 - yaazz - Thu Apr 03, 2008 10:00 pm</h4>
    <div class="postbody"><span class="postbody">Ok here is my code so far, Im trying to make the ball bounce around the edges of the screen, but since i have added gloabal variables, it doesnt work anymore, does anyone see anything wrong?
<br/>
<br/>
</span><table align="center" border="0" cellpadding="3" cellspacing="1" width="90%"><tr> <td><span class="genmed"><b>Code:</b></span></td> </tr> <tr> <td class="code">
<br/>
<br/>
<br/>
<br/>
.arm
<br/>
.text
<br/>
.global main
<br/>
<br/>
main:
<br/>
<br/>
<br/>
mov r0, #0x4000000   @set up video control register
<br/>
mov r1, #0x400 
<br/>
add r1, r1, #3
<br/>
str r1, [r0]
<br/>
@/////////////////////////////////
<br/>
<br/>
<br/>
<br/>
      
<br/>
drawScreen:
<br/>
   ldr r3,=ballx    @load in ball x and ball y
<br/>
   ldr r3,[r3]
<br/>
<br/>
   ldr r4,=bally
<br/>
   ldr r4,[r4]
<br/>
    
<br/>
<br/>
mov r5, #5       @W = 10
<br/>
mov r7, #5       @H = 50
<br/>
<br/>
loop1:
<br/>
    ldr r0, =0x6000000  @goto vram
<br/>
    ldr r1, =0x659E     @Go to pinkish Color
<br/>
    mov r2, #0x1E0      @width of screen
<br/>
    
<br/>
<br/>
    
<br/>
    mul r2, r2, r4   @curposition = Y * Width
<br/>
    add r2, r2, r3   @curposition += X
<br/>
<br/>
<br/>
<br/>
loop2:
<br/>
   strh r1, [r0,r2]  @ will store the 16bit value in r1 into address in r0, then
<br/>
    add r2,r2,#2      @ add 16bits to r6
<br/>
    subs r5, r5, #0x1 @ subtract 1 from W
<br/>
        bne loop2         @ if W!=0 goto loop2   
<br/>
<br/>
    mov r5, #0x5     @Reset the W variable
<br/>
    add r4,r4,#1
<br/>
    subs r7, r7, #1    @add 1 to Y variable
<br/>
        bne loop1         @if not equal goto loop 1
<br/>
<br/>
    mov r7,#5
<br/>
<br/>
<br/>
moveBall:
<br/>
    ldr r0, =ballx
<br/>
    ldr r0, [r0]        @load ballx in r0
<br/>
<br/>
    ldr r2, =ballSpdx
<br/>
    ldr r2, [r2]        @load ballspdx in r0
<br/>
<br/>
    mov r3,#-1          @Change sign of ball speed if needed
<br/>
    adds r0,r0,r2       @ballx += ballspeedx
<br/>
    muleq r2,r3,r2      @if x=0 ballspdx=-x
<br/>
    
<br/>
    cmp r0,#480         @if ball=240 * 2bpp
<br/>
    muleq r2,r3,r2      @ballspdx = -ballspdx
<br/>
    
<br/>
    ldr r4, =ballx   @store ball x
<br/>
    str r0, [r4]
<br/>
<br/>
    ldr r4, =ballSpdx @store ball speedx
<br/>
    str r1, [r4]
<br/>
<br/>
<br/>
<br/>
clrScreenEntry:
<br/>
    mov r0,#0x6000000
<br/>
    mov r1,#0x0
<br/>
    ldr r2,=38400
<br/>
clrScreen:
<br/>
    
<br/>
    strh r1, [r0]  @ will store the 16bit value in r1 into address in r0, then
<br/>
    add r0,r0,#2      @ add 16bits to r6
<br/>
    subs r2, r2, #0x1 @ subtract 1 from r2
<br/>
        bne clrScreen         @ if W!=0 goto loop2   
<br/>
            b drawScreen
<br/>
   
<br/>
    
<br/>
@ Global Variables
<br/>
<br/>
    .data                @ Data section (IWRAM)
<br/>
   // .align 1             @ Alignment for a halfword
<br/>
    .global ballx        @ Make it global
<br/>
ballx:
<br/>
    .word   10          @ Allocate a halfword containing 12
<br/>
    
<br/>
<br/>
                   @ Data section (IWRAM)
<br/>
    //.align 1             @ Alignment for a halfword
<br/>
    .global bally      @ Make it global
<br/>
bally:
<br/>
    .word   10          @ Allocate a halfword containing 12
<br/>
    
<br/>
<br/>
<br/>
                  @ Data section (IWRAM)
<br/>
   // .align 1             @ Alignment for a halfword
<br/>
    .global ballSpdx      @ Make it global
<br/>
ballSpdx:
<br/>
    .word   3          @ Allocate a halfword containing 12
<br/>
    
<br/>
<br/>
                    @ Data section (IWRAM)
<br/>
    //.align 1             @ Alignment for a halfword
<br/>
    .global ballSpdy      @ Make it global
<br/>
ballSpdy:
<br/>
    .word   3          @ Allocate a halfword containing 12
<br/>
    
<br/>
<br/>
<br/>
</td> </tr></table><span class="postbody"></span><span class="gensmall"></span></div>    
</div>
<div class="post">
    <h4>#153694 - Dwedit - Fri Apr 04, 2008 12:35 am</h4>
    <div class="postbody"><span class="postbody">.word does not allocate a halfword<br/>_________________<br/>"We are merely sprites that dance at the beck and call of our button pressing overlord."</span><span class="gensmall"></span></div>    
</div>
<div class="post">
    <h4>#153727 - yaazz - Fri Apr 04, 2008 4:10 pm</h4>
    <div class="postbody"><span class="postbody">Ooops I should have changed that comment.They are supposed to be words, I copied and pasted from Cearn's code above but changed .hword to word</span><span class="gensmall"></span></div>    
</div>
<div class="post">
    <h4>#153733 - Miked0801 - Fri Apr 04, 2008 7:09 pm</h4>
    <div class="postbody"><span class="postbody">Ok, I speak for all experienced coders here when I say:
<br/>
<br/>
Comments are as important as code - even more so when dealing in assembly language.  PLEASE, take the time to go through your code and clean up the comments - especially before asking strangers for help.  Once you do that, you'll get some more insightful responses.</span><span class="gensmall"></span></div>    
</div>
<div class="post">
    <h4>#153864 - Cearn - Mon Apr 07, 2008 3:13 pm</h4>
    <div class="postbody"><span class="postbody"></span><table align="center" border="0" cellpadding="3" cellspacing="1" width="90%"><tr> <td><span class="genmed"><b>yaazz wrote:</b></span></td> </tr> <tr> <td class="quote">Ok here is my code so far, Im trying to make the ball bounce around the edges of the screen, but since i have added global variables, it doesnt work anymore, does anyone see anything wrong?
<br/>
<br/>
<table align="center" border="0" cellpadding="3" cellspacing="1" width="90%"><tr> <td><span class="genmed"><b>Code:</b></span></td> </tr> <tr> <td class="code">&lt;&lt;stuff&gt;&gt;
<br/>
</td> </tr></table><span class="postbody"></span></td> </tr></table><span class="postbody">
<br/>
The code is mostly functional, but there are some small errors are one large efficiency problem that makes it hard to see anything. (And then there's the comments as Miked0801 said. If the comments and code disagree, they're both wrong)
<br/>
<br/>
First, the reason that the balls don't bounce.
<br/>
</span><table align="center" border="0" cellpadding="3" cellspacing="1" width="90%"><tr> <td><span class="genmed"><b>yaazz wrote:</b></span></td> </tr> <tr> <td class="quote"><table align="center" border="0" cellpadding="3" cellspacing="1" width="90%"><tr> <td><span class="genmed"><b>Code:</b></span></td> </tr> <tr> <td class="code">   ldr r0, =ballx
<br/>
   ldr r0, [r0]      @load ballx in r0
<br/>
<br/>
   ldr r2, =ballSpdx
<br/>
   ldr r2, [r2]      @load ballspdx in r0
<br/>
<br/>
   mov r3,#-1         @Change sign of ball speed if needed
<br/>
   adds r0,r0,r2      @ballx += ballspeedx
<br/>
   muleq r2,r3,r2      @if x=0 ballspdx=-x
<br/>
   
<br/>
   cmp r0,#480       @if ball=240 * 2bpp
<br/>
   muleq r2,r3,r2      @ballspdx = -ballspdx
<br/>
   
<br/>
   ldr r4, =ballx    @store ball x
<br/>
   str r0, [r4]
<br/>
<br/>
   ldr r4, =ballSpdx @store ball speedx
<br/>
   str r1, [r4]</td> </tr></table><span class="postbody"></span></td> </tr></table><span class="postbody">
<br/>
First, you're checking for equalities here ("muleq"). Since 10+<span style="font-style: italic">k</span>*3 will never be 0 or 480, the test always fails. What you need is 'lower than' (<span style="font-style: italic">lt</span>) and 'greater than/equal to' (<span style="font-style: italic">ge</span>). You're also writing the wrong register back to ballSpdx (should be r2, not r1). I'll assume you're not handling y-changes because you're saving that for later.
<br/>
<br/>
A second problem is that there's no timing signal to ensure a consistent frame rate (wait for VBlank, for example). Without this, the CPU will just blaze through the code as fast as possible. Now, it will <span style="font-style: italic">seem</span> like  there is some sort of timing because of your method of clearing the screen:
<br/>
</span><table align="center" border="0" cellpadding="3" cellspacing="1" width="90%"><tr> <td><span class="genmed"><b>yaazz wrote:</b></span></td> </tr> <tr> <td class="quote"><table align="center" border="0" cellpadding="3" cellspacing="1" width="90%"><tr> <td><span class="genmed"><b>Code:</b></span></td> </tr> <tr> <td class="code">
<br/>
clrScreen:
<br/>
    strh r1, [r0]           @ Nc + Nd
<br/>
    add r0,r0,#2            @ S
<br/>
    subs r2, r2, #0x1       @ S
<br/>
    bne clrScreen           @ 2S + Nc
<br/>
<br/>
    @ Total : 2 Nc + Nd + 4S = 2*8 + 1 + 4*6 = 41
<br/>
</td> </tr></table><span class="postbody"></span></td> </tr></table><span class="postbody">This loop is in ROM, as ARM code, filling one halfword at a time. This is the <span style="font-style: italic">worst</span> combination of factors for VRAM clearing you can devise without actually trying. Each iteration of this loop takes 41 cycles, for a total of 41*240*160 = 1.57 Mcycles, or 5.6 frames. In other words, the vast majority of time in your code is currently spent erasing everything. Halfword fills are bad, very bad, here.
<br/>
<br/>
There are many, many ways of cutting down on the fill time. For example, using word-fills would cut that time in half already. For the best result, use CpuFastSet (swi 12). This uses octuple stmia's and the routine is in in faster memory (BIOS) to boot. CpuFastSet takes 1.3 cycles per halfword, giving a speed-up of about 30x.
<br/>
   CpuFastSet does have some alignment and size requirements though (see <a class="postlink" href="http://nocash.emubase.de/gbatek.htm#biosmemorycopy" target="_blank">GBATek:CpuFastSet</a>). For general 16-bit fills, consider using <a class="postlink" href="http://www.coranac.com/2007/11/09/new-memset16-routine/" target="_blank">this</a>. This routine should beat manual loops after about 6 halfwords or so.
<br/>
<br/>
There are other, smaller efficiency issues as well. For example, negating a sign can be done without multiplication. There is an RSB instruction for a Reverse SuBtraction. "rsb r0, r1, #0" equates to "r0 = 0 - r1", or "r0= -r1".
<br/>
<br/>
You should also try to limit memory accesses as much as possible. That means don't loading the variable addresses, using relative addressing from a base register, and keeping relevant data in registers as much as possible. For example, you have <span style="font-style: italic">ballx</span>, <span style="font-style: italic">bally</span>, <span style="font-style: italic">balldx</span> and <span style="font-style: italic">balldy</span>, and these items are adjacent in memory. So once you know one address, you have all of them. You can even use ldm instead of ldr to load multiple variables in one go. 
<br/>
</span><table align="center" border="0" cellpadding="3" cellspacing="1" width="90%"><tr> <td><span class="genmed"><b>Code:</b></span></td> </tr> <tr> <td class="code">
<br/>
    @ Load address of ballx
<br/>
    ldr     r0,=ballx
<br/>
   
<br/>
   @ Load ballx and bally via relative addressing
<br/>
    ldr     r4, [r0]
<br/>
    ldr     r5, [r0, #4]
<br/>
<br/>
   @ Load ballx and bally ldmia. Same as above, but faster.
<br/>
   ldmia    r0, {r4-r5}
<br/>
   
<br/>
    ...
<br/>
ballx:
<br/>
    .word   10
<br/>
bally:
<br/>
    .word   10
<br/>
</td> </tr></table><span class="postbody">
<br/>
This is actually how C structs are implemented as well. There is a label to some part of memory, followed by the different members. To load a member, you get the base pointer/address and use relative addressing to get the member you want, whether it's a byte, word, or anything.
<br/>
<br/>
And then there's rendering of the ball itself. Your rendering loop is twice the size that it should be, and there should be no multiplications or memory loads inside it. If you have data that doesn't actually change inside the loops (i.e., it's "loop invariant"), take it out of the loop! Where possible, also replace multiplies by incremental additions. This is exactly what Miked0801's code did, so use that as an example. You will have to change "strh r1,[r0]!" to "strh r1,[r0], #2". Quick addressing/write-back overview:
<br/>
</span><table align="center" border="0" cellpadding="3" cellspacing="1" width="90%"><tr> <td><span class="genmed"><b>Code:</b></span></td> </tr> <tr> <td class="code">@ Pre-indexed addressing
<br/>
ldr     Rd, [Rm, #4]    Rd = *(u32*)(Rm+4)
<br/>
<br/>
@ Pre-indexed, with writeback
<br/>
ldr     Rd, [Rm, #4]!   Rd = *(u32*)(Rm+4)  ; Rm += 4
<br/>
<br/>
@ Post-indexed with writeback
<br/>
ldr     Rd, [Rm], #4    Rd = *(u32*)(Rm)    ; Rm += 4
<br/>
</td> </tr></table><span class="postbody">
<br/>
As you can see, updating pointers don't need a separate instruction in ARM; you can use write-backs. So using "strh r3, [r0]; add r0, r0, #2" can be done in one instruction: "strh r3, [r0], #2".
<br/>
<br/>
The code below if pretty much what it ought to be, including stacking for used registers so that it can be safely called from C code. There are some sneaky bits, but I hope most of it is understandable. Note that there is a pre-processor switch for vsynching with interrupts, because I'm not sure you have interrupts enabled right now. Also, the collision detection is still flawed. Correcting it has been left as an exercise for the reader. 
<br/>
<br/>
</span><table align="center" border="0" cellpadding="3" cellspacing="1" width="90%"><tr> <td><span class="genmed"><b>Code:</b></span></td> </tr> <tr> <td class="code">    .text
<br/>
    .arm
<br/>
    .align
<br/>
    .global bouncer
<br/>
<br/>
bouncer:
<br/>
    @ Save clobbered registers (r0-r3,ip are free)
<br/>
    stmfd   sp!, {r4-r7, lr}
<br/>
<br/>
    mov r0, #0x4000000   @ Set up video control register
<br/>
    mov r1, #0x400
<br/>
    add r1, r1, #3
<br/>
    str r1, [r0]
<br/>
<br/>
    @ --- This is the main loop ---
<br/>
.LmainLoop:
<br/>
    
<br/>
    @ --- Register list ---
<br/>
    @ r0 : sourcy stuff
<br/>
    @ r1 : desty stuff
<br/>
    @ r2 : countery stuff
<br/>
    @ r3 : datay stuff / temp variable
<br/>
    @ r4 : ball.x
<br/>
    @ r5 : ball.y
<br/>
    @ r6 : ball.w; later ball.dx
<br/>
    @ r7 : ball.h; later ball.dy
<br/>
    @ ip : &amp;ball
<br/>
<br/>
    ldr     ip,=ball            @ load ball
<br/>
    ldmia   ip!, {r4-r5}        @ ball.x, ball.y
<br/>
<br/>
    mov     r6, #5              @ ball.w
<br/>
    mov     r7, #5              @ ball.h
<br/>
<br/>
    @ --- Rendering the ball ---
<br/>
<br/>
    @# NOTE: do as much outside of the loops as possible
<br/>
    @# The following instructions are preparation steps that 
<br/>
    @# represent 'loop invariant' quantities: things that 
<br/>
    @# don't change inside the loop.
<br/>
    @#
<br/>
    @# * Get a pointer to a corner of the destination 
<br/>
    @#   (in this case the top-left). 
<br/>
    @# * Instead of multiplying y*pitch, use incremental 
<br/>
    @#   offsets : dst += pitch
<br/>
    @# * That said, as the inner loop increments the dst pointer
<br/>
    @#   the distance to the next scanline is ball.w less 
<br/>
    @#   then the actual pitch. This can be calculated outside the 
<br/>
    @#   loop as well.
<br/>
<br/>
    ldr     r0,=0x659E          @ Pinkish color
<br/>
<br/>
    @ u16 *dst = &amp;vram_16[y*240+x] = VRAM + y*(512-32) + x*2
<br/>
    ldr     r1,=0x6000000
<br/>
    add     r1, r1, r5, lsl #9
<br/>
    sub     r1, r1, r5, lsl #5
<br/>
    add     r1, r1, r4, lsl #1
<br/>
<br/>
    rsb     r3, r7, #240        @ screen.w - ball.w
<br/>
<br/>
.LloopY:
<br/>
        mov     r2, r6          @ Reload ball.w
<br/>
.LloopX:
<br/>
            strh    r0, [r1], #2
<br/>
            subs    r2, r2, #1
<br/>
            bne     .LloopX
<br/>
<br/>
        add     r1, r1, r3, lsl #1
<br/>
        subs    r7, r7, #1
<br/>
        bne     .LloopY
<br/>
<br/>
    @ --- Ball movement and collision detection (faulty) ---
<br/>
<br/>
    ldmia   ip!, {r6-r7}        @ Load ball.dx, ball.dy
<br/>
<br/>
    @ x += dx; if(x&lt;0 || x&gt;=240) dx= -dx;
<br/>
    adds    r4, r4, r6
<br/>
    rsblt   r6, r6, #0
<br/>
    cmp     r4, #240
<br/>
    rsbge   r6, r6, #0
<br/>
<br/>
    @ y += dy; if(y&lt;0 || y&gt;=160) dy= -dy;
<br/>
    adds    r5, r5, r7
<br/>
    rsblt   r7, r7, #0
<br/>
    cmp     r5, #160
<br/>
    rsbge   r7, r7, #0
<br/>
<br/>
    stmdb   ip!, {r4-r7}        @ Save x, y, dx, dy
<br/>
<br/>
    @ --- VBlank synchronization ---
<br/>
    @# NOTE : you always need a timing signal for consistent 
<br/>
    @# framerates. This is usually the VBlank
<br/>
<br/>
#if (I_CAN_HAS_INTERRUPTS==1)
<br/>
<br/>
    @ VBlankIntrWait. Only use if interrupts work properly
<br/>
    swi     #0x50000
<br/>
<br/>
#else
<br/>
<br/>
    @ V-synch via REG_VCOUNT
<br/>
    mov     r0, #0x04000000
<br/>
.LwaitForVDraw:
<br/>
        ldrh    r3, [r0, #6]
<br/>
        cmp     r3, #160
<br/>
        bge     .LwaitForVDraw
<br/>
                
<br/>
.LwaitForVBlank:
<br/>
        ldrh    r3, [r0, #6]
<br/>
        cmp     r3, #160
<br/>
        blt     .LwaitForVBlank
<br/>
<br/>
#endif
<br/>
<br/>
    @ --- Clear screen with CpuFastSet ---
<br/>
    @# CpuFastSet is the fastest way to fill large chunks of 
<br/>
    @# memory, but comes with requirements. Look it up in GBATek.
<br/>
<br/>
    mov     r0, #0
<br/>
    stmfd   sp!, {r0}           @ Reserve a place for zero on the stack.
<br/>
    mov     r0, sp              @ Source-ptr
<br/>
    mov     r1, #0x06000000     @ Destination
<br/>
    ldr     r2,=0x01004B00      @ Count + fill-bit: 240*160/2 + BIT(24)
<br/>
    swi     #0xC0000            @ Clear screen
<br/>
    add     sp, sp, #4          @ 'pop' zero off again
<br/>
<br/>
    b .LmainLoop
<br/>
<br/>
    ldmfd   sp!, {r4-r7, lr}    @ Restore used registers
<br/>
    bx  lr
<br/>
</td> </tr></table><span class="postbody">
<br/>
C driver if necessary:
<br/>
</span><table align="center" border="0" cellpadding="3" cellspacing="1" width="90%"><tr> <td><span class="genmed"><b>Code:</b></span></td> </tr> <tr> <td class="code">
<br/>
void bouncer();
<br/>
<br/>
int main()
<br/>
{
<br/>
    //# Set-up interrupts and whatever
<br/>
<br/>
    bouncer();
<br/>
    return 0;
<br/>
}
<br/>
</td> </tr></table><span class="postbody">
<br/>
Many of the optimizations I've used here could already be done in C. Coding in assembly should only be done when you can write code that's faster than what the compiler can create, but you can only do that effectively if you already know some of the basic techniques. It may help to learn more about <a class="postlink" href="http://en.wikipedia.org/wiki/Compiler_optimization" target="_blank">common optimization techniques</a> before writing your own asm routines. The various loop optimizations are especially useful here.
<br/>
<br/>
Also look at the compiler-generated assembly. It already knows many clever tricks that you might not, and has the benefit of writing functionally correct code (if it didn't, people would get very cranky). Having said that, it does still do a few silly things, most notably in loops. These things tend to be obvious, though.</span><span class="gensmall"></span></div>    
</div>
</div>

    </body>
</html>
